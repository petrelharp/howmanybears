// set up a simple neutral simulation
initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(dimensionality="xy");
	initializeTreeSeq();
	initializeSex("A");
	
	// spatial distance units are in kilometers
	defineConstant("K", 1.0); // carrying-capacity per unit square (roughly); total area is about 43,000
	defineConstant("SD", 10.0); // sigma_D, the dispersal distance
	defineConstant("SI", 10.0); // sigma_I, the spatial interaction distance
	defineConstant("SM", 10.0); // sigma_M, the mate choice distance
	defineConstant("L", 3); // mean lifetime at stationarity
	defineConstant("G", 1e8); // genome length
	defineConstant("FECUN", 1/L); // mean fecundity
	defineConstant("RHO", FECUN/((1+FECUN) * K)); // constant in spatial competition function
	defineConstant("REPRO_AGE", 2);
	defineConstant("NUM_NEIGHBORS", 5);
	
	initializeMutationRate(1e-7);
	
	// m1 mutation type: neutral
	initializeMutationType("m1", 0.5, "f", 0.0);
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	
	// uniform chromosome of length 100 kb with uniform recombination
	initializeGenomicElement(g1, 0, G-1);
	initializeRecombinationRate(1e-8);
	
	// spatial competition
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=SI * 3);
	i1.setInteractionFunction("n", 1.0/(2*PI*SI^2), SI);
	
	// mate choice
	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=SM * 3);
	i2.setInteractionFunction("n", 1.0/(2*PI*SM^2), SM);
}

reproduction(p1, "F") {
	if (individual.age >= REPRO_AGE) {
		mates = i2.drawByStrength(individual, NUM_NEIGHBORS);
		mates = mates[mates.sex == "M"];
		if (mates.size()) {
			mate = sample(mates, 1);
			nOff = rpois(1, FECUN);
			for (i in seqLen(nOff)) {
				xy = individual.spatialPosition;
				nxy = p1.pointReflected(xy + rnorm(2, 0, SD));
				if (p1.spatialMapValue('up', nxy) > 0) {
					offspring = subpop.addCrossed(individual, mate);
					offspring.setSpatialPosition(nxy);
				}
			}
		}
	}
	return;
}


1 early() {
	sim.addSubpop("p1", asInteger(0.5*530*265*0.3*K));
	p1.setSpatialBounds(c(0, 0, 530, 265));
	mapValues = asFloat(strsplit(readFile("layers/up_raster.txt"), " "));
	p1.defineSpatialMap("up", "xy", c(200, 100), mapValues, valueRange=c(0.0, 4.0), colors=c("#888888", "#FFFFFF"));
	for (ind in p1.individuals) {
		xy = p1.pointUniform();
		val = p1.spatialMapValue('up', xy);
		while (val == 0) {
			xy = p1.pointUniform();
			val = p1.spatialMapValue('up', xy);
		}
		ind.tagF = val;
		ind.setSpatialPosition(xy);
	}
}

early() {
	// year-to-year movements
	for (ind in p1.individuals) {
		xy = ind.spatialPosition;
		nxy = p1.pointReflected(xy + rnorm(2, 0, SD));
		val = p1.spatialMapValue('up', nxy);
		while (val == 0) {
			nxy = p1.pointReflected(xy + rnorm(2, 0, SD));
			val = p1.spatialMapValue('up', nxy);
		}
		ind.tagF = val;
		ind.setSpatialPosition(nxy);
	}
}

early() {
	i1.evaluate(p1);
	inds = p1.individuals;
	competition = i1.totalOfNeighborStrengths(inds);
	inds.fitnessScaling = pmin(0.9, 1/(1 + RHO * inds.tagF * competition));
}

1: late() {
	// to be ready for mate choice
	i2.evaluate(p1);
}

450: late() {
	sim.treeSeqRememberIndividuals(p1.individuals);
}

500 late() { 
	sim.treeSeqOutput("bears.trees");
}
